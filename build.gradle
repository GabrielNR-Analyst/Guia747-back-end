plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.0' apply false
    id 'io.spring.dependency-management' version '1.1.7'
    id 'checkstyle'
    id 'jacoco'
}

allprojects {
    group = 'com.guia747'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'checkstyle'
    apply plugin: 'jacoco'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencyManagement {
        dependencies {
            dependency 'org.mapstruct:mapstruct:1.6.3'
            dependency 'org.mapstruct:mapstruct-processor:1.6.3'
            dependency 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.8'
        }
    }

    test {
        useJUnitPlatform()
    }

    checkstyle {
        toolVersion = "10.12.4"
        configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")
    }

    jacoco {
        toolVersion = "0.8.13"
        reportsDirectory = layout.buildDirectory.dir("reports/jacoco")
    }

    jacocoTestReport {
        dependsOn test
        reports {
            html.required = true
            xml.required = true
            html.outputLocation = layout.buildDirectory.dir("jacocoHtml")
        }
    }

}
